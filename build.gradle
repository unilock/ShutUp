plugins {
	alias libs.plugins.quilt.loom
	id 'maven-publish'
}

version = "${project.version}+${libs.versions.minecraft.get()}+quilt"
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven { url = "https://maven.bawnorton.com/releases/" }
	maven { url = "https://mvn.devos.one/releases/" }
	maven { url = "https://mvn.devos.one/snapshots/" }

	exclusiveContent {
		forRepository {
			maven {
				name = "CurseForge"
				url = "https://cursemaven.com/"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven/"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}

	maven { url = "https://jitpack.io/" }
}

loom {
    mods {
		"shutup" {
			sourceSet("main")
		}
	}
}

dependencies {
	minecraft libs.minecraft
	mappings variantOf(libs.quilt.mappings) { classifier 'intermediary-v2' }
	modImplementation libs.quilt.loader
	modImplementation libs.quilted.fabric.api

	annotationProcessor(implementation(include("com.github.bawnorton.mixinsquared:mixinsquared-fabric:0.3.4")))

	modImplementation("io.github.fabricators_of_create.Porting-Lib:entity:2.3.13+1.20.1") { exclude group: "net.fabricmc.fabric-api" }
	modImplementation("io.github.fabricators_of_create.Porting-Lib:obj_loader:2.3.13+1.20.1") { exclude group: "net.fabricmc.fabric-api" }
	modImplementation "maven.modrinth:aquamirae:ouUaC54p"
	modImplementation "maven.modrinth:aurorasdecorations:1.0.0-beta.26+1.20.1"
	modImplementation "curse.maven:expanded-storage-978068:5270227" //10.3.0-beta.5+1.20.1-quilt
	modImplementation "maven.modrinth:fzzy-core:0.5.1+1.20.1"
	modImplementation "maven.modrinth:jsonpatcher:1.0.0-beta.4+mc.1.20.1"
	modImplementation "maven.modrinth:kawaii-dishes:1.12.0"
	modImplementation "curse.maven:loot-integrations-580689:6640997"
	modImplementation "maven.modrinth:nbt-autocomplete:PX15OQcc"
	modImplementation "maven.modrinth:obscure-api:16"
	modImplementation "maven.modrinth:spectrum:1.8.8"

	modCompileOnly(files("libs/excorrelation-1.1.46-dirty.jar"))

	// sigh...
	implementation "maven.modrinth:dynamicsurroundingsfabric:0.3.3"
	implementation "maven.modrinth:jei:15.20.0.118"
}

processResources {
	inputs.properties "version": project.version, "group": project.group

	filesMatching("quilt.mod.json") {
		expand "version": project.version, "group": project.group
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${base.archivesName.get()}" }
	}
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
